import org.gradle.internal.jvm.Jvm

plugins {
    id 'java-library'
    id 'com.bmuschko.nexus' version '2.3.1'
    id 'io.codearte.nexus-staging' version '0.8.0'
}

group = 'com.imsweb'
version = file('VERSION').text.trim()
description = 'Utility library for SEER Java projects.'
sourceCompatibility = JavaVersion.VERSION_1_8

println "Starting build using ${Jvm.current()}"

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.apache.commons:commons-lang3:3.7'
    implementation 'commons-io:commons-io:2.6'

    testImplementation 'junit:junit:4.12'
}

// enforce UTF-8, display the compilation warnings
tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
    options.compilerArgs << '-Xlint:unchecked' << '-Xlint:deprecation'
}

// the Javadoc was made way too strict in Java 8 and it's not worth the time fixing everything!
tasks.withType(Javadoc) {
    options.addStringOption('Xdoclint:none', '-quiet')
}

// customize the manifest
jar {
    manifest {
        attributes('Implementation-Title': project.name,
                'Implementation-Version': project.version,
                'Implementation-Vendor': 'Information Management Services Inc.',
                'Created-By': System.properties['java.vm.version'] + ' (' + System.properties['java.vm.vendor'] + ')',
                'Built-By': System.getProperty('user.name'),
                'Built-Date': new Date(),
                'Built-JDK': System.getProperty('java.version'),
                'Automatic-Module-Name': 'com.imsweb.seerutils'
        )
    }
}

// needed to deploy to Maven Central
modifyPom {
    project {
        name 'SEER*Utils'
        description 'Generic utility classes for SEER Java projects.'
        url 'https://github.com/imsweb/seerutils'
        inceptionYear '2018'

        scm {
            url 'https://github.com/imsweb/seerutils'
            connection 'scm:https://github.com/imsweb/seerutils.git'
            developerConnection 'scm:git@github.com:imsweb/seerutils.git'
        }

        licenses {
            license {
                name 'A modified BSD License (BSD)'
                url 'https://github.com/imsweb/seerutils/blob/master/LICENSE'
                distribution 'repo'
            }
        }

        developers {
            developer {
                id 'depryf'
                name 'Fabian Depry'
                email 'depryf@imsweb.com'
            }
        }
    }
}

// don't try to release a snapshot to a non-snapshot repository, that won't work anyway
if (version.endsWith('-SNAPSHOT'))
    gradle.startParameter.excludedTaskNames += 'closeAndReleaseRepository'

// Gradle wrapper, this allows to build the project without having to install Gradle!
wrapper {
    gradleVersion = '5.5.1'
    distributionType = Wrapper.DistributionType.ALL
}




